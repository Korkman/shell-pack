#! /usr/bin/env fish

# dool requires plugins in either global locations or a subdirectory "plugins"
# adjacent to its binary. we aim for the latter, but don't want bin/plugins,
# so we created bin/dool.d/plugins, which places bin/dool.d/dool outside of PATH.
# since we don't want to inflate PATH anymore, and a symlink doesn't work,
# we install this script as bin/dool to proxy around.

# loading shell-pack as a library
# this does not load keybinds for non-interactive cases
load_shell_pack

set realfilename (realpath (status filename))
set scriptname (status basename)

# despite all caution, infinite loops can still happen for weird setups (like my dev station)
if set -q __sp_dool_infinite_loop_breaker
	echo "Breaking infinite loop (sorry, please check your dool installation, see also 'command -a dool') ..."
	exit 20
end
set -x __sp_dool_infinite_loop_breaker 1

# if dstat is available, run it - it might be a different tool (the Red Hat desaster)
if [ "$scriptname" = "dstat" ]
	for search in (command -a dstat)
		if [ (realpath "$search") != "$realfilename" ]
			exec "$search" $argv
			exit 10 # this should never execute
		end
	end
end

# if downloaded dool is available, run it
if [ -x "$__sp_dir/bin/dool.d/dool" ]
	#echo "exec local dool"
	exec "$__sp_dir/bin/dool.d/dool" $argv
	exit 10 # this should never execute
else
	# try find globally installed dool, run that
	for search in (command -a dool)
		if [ (realpath "$search") != "$realfilename" ]
			exec "$search" $argv
			exit 10 # this should never execute
		end
	end
	# if not available, fall back to any installed dstat
	for search in (command -a dstat)
		if [ (realpath "$search") != "$realfilename" ]
			exec "$search" $argv
			exit 10 # this should never execute
		end
	end
	echo "No dool (or dstat) executable found in PATH or \"$__sp_dir/bin/dool.d\""
	exit 1
end
